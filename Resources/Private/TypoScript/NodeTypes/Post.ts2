prototype(ObisConcept.NeosBlog:Post) < prototype(TYPO3.Neos:Document) {
  _hiddenInIndex = TRUE

  content = TYPO3.Neos:ContentCollection {
    nodePath = 'content'
  }
}

prototype(ObisConcept.NeosBlog:PostList) < prototype(TYPO3.Neos:Content) {
  @override.templatePathPostList = 'resource://ObisConcept.NeosBlog/Private/Templates/NodeTypes/PostList.html'
  @override.templatePathPostItem = 'resource://ObisConcept.NeosBlog/Private/Templates/TypoScriptObjects/PostItem.html'

  templatePath = ${templatePathPostList}

  @override.blogFolderNode = ${q(node).property('blogFolderNode')}
  postCollection = ${q(blogFolderNode).is('[instanceof ObisConcept.NeosBlog:Folder]') ? q(blogFolderNode).children('[instanceof ObisConcept.NeosBlog:Post]').get() : NULL}

  @override.categoryList = ${q(node).property('categories') != NULL ? Array.join(q(node).categoryListByProperty('categories')) : ''}
  postCollection.@process.filterPostsByCategories = ${categoryList != NULL && categoryList != '' ? q(value).filter('[categories *= \'' + categoryList + '\']').get() : value}

  postItem = ObisConcept.NeosBlog:Post {
    templatePath = ${templatePathPostItem}
  }

  @cache {
    mode = 'uncached'
    context {
      1 = 'node'
      2 = 'documentNode'
      3 = 'site'
    }
  }
}
